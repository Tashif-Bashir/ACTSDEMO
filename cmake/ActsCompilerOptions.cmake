# Acts compiler flags
if (NOT CMAKE_BUILD_TYPE)
  set(CMAKE_BUILD_TYPE RelWithDebInfo CACHE STRING "Build type configuration" FORCE)
  message(STATUS "Setting default build type: ${CMAKE_BUILD_TYPE}")
endif() 

set(ACTS_CXX_FLAGS "-Wall -Wextra -Wpedantic -Wshadow -Wunused-local-typedefs")
set(ACTS_CXX_FLAGS_DEBUG "--coverage")
set(ACTS_CXX_FLAGS_MINSIZEREL "")
set(ACTS_CXX_FLAGS_RELEASE "")
set(ACTS_CXX_FLAGS_RELWITHDEBINFO "")

# Acts linker flags
set(ACTS_EXE_LINKER_FLAGS_DEBUG "--coverage")
set(ACTS_SHARED_LINKER_FLAGS_DEBUG "--coverage ")

# assign to global CXX flags
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${ACTS_CXX_FLAGS}")
set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} ${ACTS_CXX_FLAGS_DEBUG}")
set(CMAKE_CXX_FLAGS_MINSIZEREL "${CMAKE_CXX_FLAGS_MINSIZEREL} ${ACTS_CXX_FLAGS_MINSIZEREL}")
set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} ${ACTS_CXX_FLAGS_RELEASE}")
set(CMAKE_CXX_FLAGS_RELWITHDEBINFO "${CMAKE_CXX_FLAGS_RELWITHDEBINFO} ${ACTS_CXX_FLAGS_RELWITHDEBINFO}")

# assign to global linker flags
set(CMAKE_EXE_LINKER_FLAGS_DEBUG "${CMAKE_EXE_LINKER_FLAGS_DEBUG} ${ACTS_EXE_LINKER_FLAGS_DEBUG}")
set(CMAKE_SHARED_LINKER_FLAGS_DEBUG "${CMAKE_SHARED_LINKER_FLAGS_DEBUG} ${ACTS_SHARED_LINKER_FLAGS_DEBUG}")

# silence warning about missing RPATH on Mac OSX
set(CMAKE_MACOSX_RPATH 1)

if(ACTS_RUN_CLANG_TIDY)
  find_program(CLANG_TIDY_COMMAND NAMES clang-tidy)
  if(NOT CLANG_TIDY_COMMAND)
    message(WARNING "CMake_RUN_CLANG_TIDY is ON but clang-tidy is not found!")
    set(CMAKE_CXX_CLANG_TIDY "" CACHE STRING "" FORCE)
  else()
    message(STATUS "Setting up clang-tidy run")

    set(_chks "")
    list(APPEND _chks "-*")
    list(APPEND _chks "readability-*")
    list(APPEND _chks "-readability-redundant-member-init")
    list(APPEND _chks "misc-*")
    list(APPEND _chks "-misc-unused-parameters")
    list(APPEND _chks "bugprone-*")
    list(APPEND _chks "performance-*")
    list(APPEND _chks "modernize-*")
    list(APPEND _chks "-modernize-use-auto")
    list(APPEND _chks "clang-analyzer-deadcode.*")
    list(APPEND _chks "clang-analyzer-*")
    list(APPEND _chks "-clang-analyzer-osx.*")
    list(APPEND _chks "-clang-analyzer-unix.*")
    list(APPEND _chks "cppcoreguidelines-*")
    list(APPEND _chks "-cppcoreguidelines-pro-type-vararg")
    list(APPEND _chks "-cppcoreguidelines-owning-memory")
    list(APPEND _chks "-cppcoreguidelines-pro-bounds-constant-array")
    list(JOIN _chks "," CLANG_TIDY_CHECKS)

    message(STATUS "Configured checks")
    foreach(_chk ${_chks})
      message(STATUS "|-> ${_chk}")
    endforeach()

    set(_errs "")
    list(APPEND _errs "readability-inconsistent-declaration-parameter-name")
    list(APPEND _errs "readability-inconsistent-declaration-parameter-name")
    list(APPEND _errs "readability-named-parameter")
    list(APPEND _errs "readability-container-size-empty")
    list(APPEND _errs "modernize-use-using")
    list(APPEND _errs "readability-braces-around-statements")
    list(APPEND _errs "modernize-use-override")
    list(APPEND _errs "modernize-use-equals-default")
    list(APPEND _errs "readability-implicit-bool-cast")
    list(APPEND _errs "readability-implicit-bool-conversion")
    list(APPEND _errs "modernize-use-default-member-init")
    list(APPEND _errs "performance-unnecessary-value-param")
    list(APPEND _errs "performance-move-const-arg")
    list(APPEND _errs "modernize-use-equals-default")
    list(APPEND _errs "modernize-use-nullptr")
    list(JOIN _errs "," CLANG_TIDY_ERRORS)

    message(STATUS "Enabled errors:")
    foreach(_err ${_errs})
      message(STATUS "|-> ${_err}")
    endforeach()

    
    set(CMAKE_CXX_CLANG_TIDY "${CLANG_TIDY_COMMAND};-checks=${CLANG_TIDY_CHECKS};-header-filter='.*';-warnings-as-errors=${CLANG_TIDY_ERRORS}")
  endif()
endif()
